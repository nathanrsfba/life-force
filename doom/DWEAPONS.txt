// Heretic-specific weapon subclasses

actor DLifeForceWeapon: LifeForceWeapon
{
	States
	{
		Start.Beam:
		STF2 A 1 Offset(0, 32)
		"####" B 1 Offset(0, 31)
		"####" C 1 Offset(0, 30)
		"####" D 1 Offset(0, 29)
		"####" E 1 Offset(0, 28)
		goto Hold.Beam

		End.Beam:
		STF2 E 1 Offset(0, 28)
		"####" D 1 Offset(0, 29)
		"####" C 1 Offset(0, 30)
		"####" B 1 Offset(0, 31)
		"####" A 1 Offset(0, 32)
		goto Ready
		
		StartMelee.Beam:
		STAF A 1 Offset(0,29)
		"####" "#" 1 Offset(0,26)
		"####" "#" 1 Offset(0,23)
		"####" "#" 1 Offset(0,20)
		"####" "#" 1 Offset(0,17)
		goto MeleePunch.Beam
		
		EndMelee.Beam:
		STAF A 1 Offset(0,17)
		"####" "#" 1 Offset(0,20)
		"####" "#" 1 Offset(0,23)
		"####" "#" 1 Offset(0,26)
		"####" "#" 1 Offset(0,29)
		goto ready
		
		Start.Fireball:
		STF3 A 1 Offset(0, 33)
		"####" B 1 Offset(0, 34)
		"####" C 1 Offset(0, 35)
		"####" D 1 Offset(0, 36)
		"####" E 1 Offset(0, 37)
		goto Hold.Fireball
		
		End.Fireball:
		STF3 E 1 Offset(0, 37)
		"####" D 1 Offset(0, 36)
		"####" C 1 Offset(0, 35)
		"####" B 1 Offset(0, 34)
		"####" A 1 Offset(0, 33)
		goto Ready

		Start.Spread:
		STF3 A 1 Offset(0, 33)
		"####" B 1 Offset(0, 34)
		"####" C 1 Offset(0, 35)
		"####" D 1 Offset(0, 36)
		"####" E 1 Offset(0, 37)
		goto Hold.Spread
		
		End.Spread:
		STF3 E 1 Offset(0, 37)
		"####" D 1 Offset(0, 36)
		"####" C 1 Offset(0, 35)
		"####" B 1 Offset(0, 34)
		"####" A 1 Offset(0, 33)
		goto Ready

		Start.SparkLight:
		STAF A 1 Offset(0, 31)
		"####" "#" 1 Offset(0, 30)
		"####" "#" 1 Offset(0, 29)
		"####" "#" 1 Offset(0, 28)
		"####" "#" 1 Offset(0, 27)
		goto Hold.SparkLight
		
		End.SparkLight:
		STAF A 1 Offset(0, 27)
		"####" "#" 1 Offset(0, 28)
		"####" "#" 1 Offset(0, 29)
		"####" "#" 1 Offset(0, 30)
		"####" "#" 1 Offset(0, 31)
		goto Ready

		Start.Hose:
		STAF A 1 Offset(0, 31)
		"####" "#" 1 Offset(0, 30)
		"####" "#" 1 Offset(0, 29)
		"####" "#" 1 Offset(0, 28)
		"####" "#" 1 Offset(0, 27)
		goto Hold.Hose
		
		End.Hose:
		STAF A 1 Offset(0, 27)
		"####" "#" 1 Offset(0, 28)
		"####" "#" 1 Offset(0, 29)
		"####" "#" 1 Offset(0, 30)
		"####" "#" 1 Offset(0, 31)
		goto Ready
		
		Start.Burner:
		STF5 A 1 Offset(0, 33)
		"####" B 1 Offset(0, 34)
		"####" C 1 Offset(0, 35)
		"####" D 1 Offset(0, 36)
		"####" E 1 Offset(0, 37)
		goto Hold.Burner

		End.Burner:
		STF3 E 1 Offset(0, 37)
		"####" D 1 Offset(0, 36)
		"####" C 1 Offset(0, 35)
		"####" B 1 Offset(0, 34)
		"####" A 1 Offset(0, 33)
		goto Ready

		Start.Lightning:
		STAF A 1 Offset(0, 33)
		"####" "#" 1 Offset(0, 34)
		"####" "#" 1 Offset(0, 35)
		"####" "#" 1 Offset(0, 36)
		"####" "#" 1 Offset(0, 37)
		"####" "#" 0 A_PlaySound( "lifeforce/lightningstart", CHAN_AUTO )
		goto Hold.Lightning

		// Hold2.Lightning:
		EndFire.Lightning:
		STAF A 1 Offset(0, 37)
		"####" "#" 1 Offset(0, 36)
		"####" "#" 1 Offset(0, 35)
		"####" "#" 1 Offset(0, 34)
		"####" "#" 1 Offset(0, 33)
		goto Ready

		Start.Decimator:
		STAF A 1 Offset(0, 33)
		"####" "#" 1 Offset(0, 34)
		"####" "#" 1 Offset(0, 35)
		"####" "#" 1 Offset(0, 36)
		"####" "#" 1 Offset(0, 37)
		goto Hold.Decimator

		Hold.Decimator:
		"####" "#" 0 A_JumpIf( health <= 25, "Safety" )
		"####" "#" 0 ACS_NamedExecuteWithResult( "LF_SubHealth", 25 )
		"####" "#" 20 A_FireCustomMissile("DecimatorProjectile")
		"####" "#" 0 A_Refire( "Hold2.Decimator" )
		"####" "#" 0 A_Jump( 256, "End.Decimator" ) // For potential inheretence

		Hold2.Decimator:
		"####" "#" 0 A_JumpIf( health <= 35, "Safety" )
		"####" "#" 0 ACS_NamedExecuteWithResult( "LF_SubHealth", 35 )
		"####" "#" 20 A_FireCustomMissile("DecimatorProjectile2")
		"####" "#" 0 A_Refire( "Hold3.Decimator" )
		"####" "#" 0 A_Jump( 256, "End.Decimator" ) // For potential inheretence

		Hold3.Decimator:
		"####" "#" 0 A_JumpIf( health <= 40, "Safety" )
		"####" "#" 0 ACS_NamedExecuteWithResult( "LF_SubHealth", 40 )
		"####" "#" 20 A_FireCustomMissile("DecimatorProjectile3")
		"####" "#" 0 A_Jump( 256, "End.Decimator" ) // For potential inheretence

		End.Decimator:
		STAF A 1 Offset(0, 37)
		"####" "#" 1 Offset(0, 36)
		"####" "#" 1 Offset(0, 35)
		"####" "#" 1 Offset(0, 34)
		"####" "#" 1 Offset(0, 33)
		goto Ready


	}
}

actor BeamSkill : DLifeForceWeapon replaces Pistol
{
	Weapon.SlotNumber 2
	Obituary "%o was burned by %k."
	Inventory.Icon BL1PA0
	
	States
	{
		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 2 )
		TNT1 A 0
		goto Super::Deselect
		
		Fire:
		goto Super::Fire.Beam
	}
}

actor DBeam: Beam replaces Beam
{
	Translation "176:191=208:223"
	States
	{
		Spawn:
		BAL1 A 0
		goto Main
	}

}

actor DBeamFade: BeamFade replaces BeamFade
{
	Translation "176:191=208:223"
	States
	{
		Spawn:
		BAL1 A 0
		goto Super::Main
	}

}

actor FireballSkill : DLifeForceWeapon replaces Shotgun
{
	Weapon.SlotNumber 3
	Weapon.SlotPriority 1
	Obituary "%o was burned by %k."
	Inventory.Icon BL2PA0
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("FireballPickup3")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 3 )
		TNT1 A 0
		goto Super::Deselect
		
		Fire:
		goto Super::Fire.Fireball
	}
}

actor DFireballPickup: FireballPickup replaces FireballPickup
{
	States
	{
		Spawn:
		BL2P AABBAABB 2 bright A_FadeOut(0.1)
		BL2P AABBAABB 2 bright A_FadeIn(0.1)
		loop
	}
}

actor FireballPickup3: DFireballPickup
{
	States
	{
		Pickup:
		TNT1 A 0 A_JumpIfInventory("FireballSkill", 1, "Upgrade")
		
		Give:
		TNT1 A 0 A_GiveInventory("FireballSkill")
	    TNT1 A 0 A_Log("Fireball. Sweet, burny goodness.")
		stop

		Upgrade:
		TNT1 A 0 A_JumpIfInventory("SpreadFireballSkill", 1, "Heal")
		TNT1 A 0 A_JumpIf( !GetCvar( "lf_sgupgrade"), "Heal")

		TNT1 A 0 A_GiveInventory("SpreadFireballSkill")
	    TNT1 A 0 A_Log("Spread Fireball. Spread 'em!")
  		stop
	}
}

// Fireballpickup with less health, dropped by sergeants
actor FireballPickup2 : DFireballPickup
{
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump( 230, "HealthOnly" )
		goto Super::Spawn
		
		HealthOnly:
		TNT1 A 0 A_SpawnItemEx("Health1")
		stop
		
		Heal:
		TNT1 A 0 A_JumpIf(CallACS("LF_AddHealth",1), "Message")
		fail
	}
}

actor DFireballProjectile: FireballProjectile replaces FireballProjectile
{
	Translation "176:191=240:247"
	
	States
	{
		Spawn:
		BAL1 A 0
		
		Main:
  		"####" AB 4 bright
  		loop
		
		Death:
		//"####" "#" 0 A_SetTranslation( "Def" )
		"####" CDE 4 bright
		stop
	}
}

actor SpreadFireballSkill: DLifeForceWeapon replaces SuperShotgun
{
	Weapon.SlotPriority 0
	Weapon.SlotNumber 3
	Inventory.Icon "BL3PA0"
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("SpreadFireballPickup")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 8 )
		TNT1 A 0
		goto LifeForceWeapon::Deselect

		Fire:
		TNT1 A 0
		goto Super::Fire.Spread
	}
}

actor DSpreadFireballPickup: SpreadFireballPickup replaces SpreadFireballPickup 
{
	States
  	{
		Spawn:
  		BL3P AABBAABB 2 bright A_FadeOut(0.1)
  		BL3P AABBAABB 2 bright A_FadeIn(0.1)
  		loop
	}
}

actor DSpreadFireballProjectile: DFireballProjectile replaces SpreadFireballProjectile
{
  	XScale 0.5
  	YScale 0.5

  	States
  	{
		Spawn:
		BAL1 ABABABAB 1 bright A_SpawnItemEx("SpreadFireballTrail")
  		loop
	}
}

actor DSpreadFireballProjectileL: SpreadFireballProjectileL replaces SpreadFireballProjectileL
{
	Translation "176:191=240:247"
	
	States
	{
		Spawn:
		BAL1 A 0
		goto Main
	}
}

actor DSpreadFireballProjectileR: SpreadFireballProjectileR replaces SpreadFireballProjectileR
{
	Translation "176:191=240:247"
	
	States
	{
		Spawn:
		BAL1 A 0
		goto Main
	}
}

actor DSpreadFireballTrail: SpreadFireballTrail replaces SpreadFireballTrail
{
	Translation "176:191=240:247"
	
	States
	{
		Spawn:
		BAL1 ABA 4 Bright
		Stop
	}
}

actor PlasmaHoseSkill : DLifeForceWeapon replaces Chainsaw
{
	Weapon.SlotNumber 1
	Weapon.SlotPriority 1
	Obituary "%o was hosed by %k."
	Inventory.Icon "PLSSA0"
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("PlasmaHosePickup")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 1 )
		goto Super::Deselect

		Fire:
		TNT1 A 0
		goto Super::Fire.Hose

	}
}

actor DPlasmaHosePickup: PlasmaHosePickup replaces PlasmaHosePickup
{
	States
	{
		Spawn:
		PL1P AABBAABB 2 bright A_FadeOut(0.1)
		PL1P AABBAABB 2 bright A_FadeIn(0.1)
		loop
	}
}

actor DPlasmaHoseProjectile: PlasmaHoseProjectile replaces PlasmaHoseProjectile
{
	States
	{
		Spawn:
		PLSS ABABABAB 2 bright
		loop

		Death:
		PLSE ABCDE 1 Bright
		Stop
	}
}

actor DPlasmaHoseProjectile2: DPlasmaHoseProjectile replaces PlasmaHoseProjectile2
{
	Damage 1
}

actor SparkLightSkill : DLifeForceWeapon replaces Chaingun
{
	Weapon.SlotNumber 4
	Obituary "%o was burned by %k."
	Inventory.Icon "BL4PA0"
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("SparkLightPickup")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 4 )
		TNT1 A 0
		goto Super::Deselect
		
		Fire:
		goto Super::Fire.SparkLight
	}
}

actor DSparkLightPickup: SparkLightPickup replaces SparkLightPickup
{
	States
	{
		Spawn:
		BL4P AAAAAAAA 2 bright A_FadeOut(0.1)
		BL4P AAAAAAAA 2 bright A_FadeIn(0.1)
		loop
	}
}

actor SparkLightPickup2 : DSparkLightPickup
{
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump( 230, "HealthOnly" )
		goto Super::Spawn
		
		HealthOnly:
		TNT1 A 0 A_SpawnItemEx("Health1")
		stop
		
		Heal:
		TNT1 A 0 A_JumpIf(CallACS("LF_AddHealth",1), "Message")
		fail
	}
}

actor DSparkLightProjectile: SparkLightProjectile replaces SparkLightProjectile
{
	States
	{
		Spawn:
		PLSS A 0 Bright
		goto Main
	}
}

actor FireburnerSkill : DLifeForceWeapon replaces RocketLauncher
{
	Weapon.SlotNumber 5
	Obituary "%o was flamed by %k."
	Inventory.Icon "FL5PA0"
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("FireburnerPickup")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 5 )
		goto Super::Deselect

		Fire:
		TNT1 A 0
		goto Super::Fire.Burner
	}
}

actor DFireburnerPickup: FireburnerPickup replaces FireburnerPickup
{
	States
	{
		Spawn:
		FL5P ABCBABCB 2 bright A_FadeOut(0.1)
		FL5P ABCBABCB 2 bright A_FadeIn(0.1)
		loop
	}
}

actor DFireBurnerProjectile: FireBurnerProjectile replaces FireBurnerProjectile
{
	+EXTREMEDEATH
	Alpha 0.5
	RenderStyle Translucent
	Scale 0.5
	
	States
	{
		Spawn:
		FIRE A 2 bright 
		FIRE BCD 2 bright 

		Fireloop:
		FIRE E 0 bright A_PlaySound("lifeforce/burnerhit")
		FIRE EEFFGGHH 2 bright A_Explode(2, 256, 0, false, 256)
		stop

		Death:
		TNT1 A 0 A_Explode(2, 256, 0, false, 256)
		FIRE ABCBCDCDEDEFEFGFGHGH 2 bright
		stop
	}
}

actor LightningBoltSkill: DLifeForceWeapon replaces PlasmaRifle
{
	Weapon.SlotNumber 6
	Obituary "%o was shocked by %k."
	Inventory.Icon "PLSEC0"
	DamageType "Lightning"
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("LightningBoltPickup")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 6 )
		goto Super::Deselect

		Fire:
		TNT1 A 0
		goto Super::Fire.Lightning

	}
}

actor DLightningBoltPickup: LightningBoltPickup replaces LightningBoltPickup
{
	States
	{
		Spawn:
		LT6P AABBCC 2 bright A_FadeOut(0.1)
		LT6P AABBCC 2 bright A_FadeIn(0.1)
		loop
	}
}

actor DLightningBeam: LightningBeam replaces LightningBeam
{
	States
	{
		Spawn:
		LBEM A 0 bright
		goto Main
	}
}

// Tone this down a bit from our (buffed-up) Heretic settings
actor DLightningSpark: LightningSpark replaces LightningSpark
{
	States
	{
		Spawn:
		LPUF A 0 bright 
		"####" "#" 4 bright A_Explode(32, 192, 0)
		"####" BCD 4 bright
		stop
	}
}

actor DecimatorSkill : DLifeForceWeapon replaces BFG9000
{
	Weapon.SlotNumber 7
	Obituary "%o was decimated by %k."
	Inventory.Icon "BL7PA0"
	// Translation "128:151=176:191", "48:79=176:191", "208:231=176:191", "160:167=176:191"	
	// Inventory.Icon "SKULC8C2"
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("DecimatorPickup")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 7 )
		goto Super::Deselect
		
		Fire:
		goto Super::Fire.Decimator
	}
}

actor DDecimatorPickup: DecimatorPickup replaces DecimatorPickup 
{
	States
	{
		Spawn:
		BL7P AAAAAAAA 2 bright A_FadeOut(0.1)
		BL7P AAAAAAAA 2 bright A_FadeIn(0.1)
		loop
	}
}

actor DDecimatorProjectile: DecimatorProjectile replaces DecimatorProjectile
{
	Scale 0.25
	Damage (7)
	Translation "128:151=176:191", "48:79=176:191", "208:231=176:191", "160:167=176:191"	
	Alpha 0.75
	RenderStyle Translucent
	SeeSound "skull/melee"

	States
	{
		Spawn:
		SKUL CCCC 4 bright
		goto Fireloop

		Fireloop:
		"####" "#" 0 A_JumpIf( reactiontime % 4 != 0, "NoSound" )
		"####" "#" 0 A_PlaySound( "lifeforce/decactive" )
		
		NoSound:
		"####" "###" 2 bright A_SeekerMissile(360, 45, SMF_LOOK | SMF_PRECISE, 256)
		"####" "#" 0 A_Countdown
		goto FireLoop

		Death:
		"####" IJK 6 bright
		stop
	}
}

actor DDecimatorProjectile2: DDecimatorProjectile replaces DecimatorProjectile2
{
	Scale 0.5
	Damage (10)
}

actor DDecimatorProjectile3: DDecimatorProjectile replaces DecimatorProjectile3
{
	Scale 0.75
	Damage (13)
}

