// Heretic-specific weapon subclasses

actor HLifeForceWeapon: LifeForceWeapon
{
	States
	{
		Melee.Beam:
		STAF A 0 A_JumpIf( health <= 1, "Safety" )
		"####" "B" 1 Offset(-10,42) 
		"####" "C" 1 Offset(-20,52)
		"####" "D" 1 Offset(-30,62)
		"####" "E" 1 Offset(-40,72)
		"####" "F" 1 Offset(-50,82)
		STFF "D" 1 Offset(80,86) A_PlaySound("mummy/attack1")
		"####" "E" 1 Offset(60,58)
		"####" "F" 1 Offset(40,34)
		"####" "D" 1 Offset(20,16)
		"####" "E" 1 Offset(1,6) ACS_NamedExecuteWithResult( "LF_SubHealth", 1 )
		"####" "#" 0 A_CustomPunch(12,0,0,"SmackPuff",128)
		"####" "F" 1 Offset(-20,16)
		"####" "D" 1 Offset(-40,34)
		"####" "E" 1 Offset(-60,58)
		"####" "F" 1 Offset(-80,86)
		"####" "#" 0 A_JumpIf( health <= 1, "Safety" )
		"####" "#" 0 A_JumpIfCloser(128, "Melee2.Beam")
		goto EndMelee.Beam
		
		Melee2.Beam:
		TNT1 A 5 A_JumpIf( health <= 1, "Safety" )
		STFF "D" 1 Offset(-80,86) A_PlaySound("mummy/attack1")
		"####" "E" 1 Offset(-60,58)
		"####" "F" 1 Offset(-40,34)
		"####" "D" 1 Offset(-20,16)
		"####" "E" 1 Offset(-1,6) ACS_NamedExecuteWithResult( "LF_SubHealth", 1 )
		"####" "#" 0 A_CustomPunch(12,0,0,"SmackPuff",128)
		"####" "F" 1 Offset(20,16)
		"####" "D" 1 Offset(40,34)
		"####" "E" 1 Offset(60,58)
		"####" "F" 1 Offset(80,86)
		goto EndMelee.Beam

		EndMelee.Beam:
		"STAF" "F" 1 Offset(-50,82)
		"####" "E" 1 Offset(-40,72)
		"####" "D" 1 Offset(-30,62)
		"####" "C" 1 Offset(-20,52)
		"####" "B" 1 Offset(-10,42) 
		goto Ready
	}
}

actor BeamSkill : HLifeForceWeapon replaces GoldWand
{
	Weapon.SlotNumber 2
	Obituary "%o was burned by %k."
	Inventory.Icon WEP2A0
	
	States
	{
		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 2 )
		TNT1 A 0
		goto Super::Deselect
		
		Fire:
		goto Super::Fire.Beam
	}
}

actor FireballSkill : HLifeForceWeapon replaces Crossbow
{
	Weapon.SlotNumber 3
	Weapon.SlotPriority 1
	Obituary "%o was burned by %k."
	Inventory.Icon WEP3A0
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("FireballPickup")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 3 )
		TNT1 A 0
		goto Super::Deselect
		
		Fire:
		goto Super::Fire.Fireball
	}
}

// This one gives the spread fireball skill when picked up a second time,
// since Heretic has no SSG equivalent
actor HFireballPickup: FireballPickup replaces FireballPickup
{
	States
	{
		Pickup:
		TNT1 A 0 A_JumpIfInventory("SpreadFireballSkill", 1, "Heal")
		TNT1 A 0 A_JumpIfInventory("FireballSkill", 1, "Upgrade")
		TNT1 A 0 A_GiveInventory("FireballSkill")
	    TNT1 A 0 A_Log("Fireball. Sweet, burny goodness.")
		stop
		
		Upgrade:
		TNT1 A 0 A_GiveInventory("SpreadFireballSkill")
	    TNT1 A 0 A_Log("Spread Fireball. Spread 'em!")
  		stop
	}
}

actor SpreadFireballSkill : HLifeForceWeapon
{
	Weapon.SlotPriority 0
	Weapon.SlotNumber 3
	Inventory.Icon "WEP8A0"
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("SpreadFireballPickup")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 8 )
		TNT1 A 0
		goto LifeForceWeapon::Deselect

		Fire:
		TNT1 A 0
		goto LifeForceWeapon::Fire.Spread
	}
}

actor PlasmaHoseSkill : HLifeForceWeapon replaces Gauntlets
{
	Weapon.SlotNumber 1
	Weapon.SlotPriority 1
	Obituary "%o was hosed by %k."
	Inventory.Icon "WEP1A0"
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("PlasmaHosePickup")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 1 )
		goto Super::Deselect

		Fire:
		TNT1 A 0
		goto Super::Fire.Hose

	}
}

actor HPlasmaHoseProjectile: PlasmaHoseProjectile replaces PlasmaHoseProjectile
{
	Translation "137:144=193:204"

	States
	{
		Spawn:
		FX01 CDCDCDCD 2 bright
		loop

		Death:
		FX01 EFGH 1 Bright
		Stop
	}
}

actor HPlasmaHoseProjectile2: HPlasmaHoseProjectile replaces PlasmaHoseProjectile2
{
	Damage 1
}

actor SparkLightSkill : HLifeForceWeapon replaces Blaster
{
	Weapon.SlotNumber 4
	Obituary "%o was burned by %k."
	Inventory.Icon WEP4A0
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("SparkLightPickup")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 4 )
		TNT1 A 0
		goto Super::Deselect
		
		Fire:
		goto Super::Fire.SparkLight
	}
}

actor HSparkLightProjectile: SparkLightProjectile replaces SparkLightProjectile
{
	Translation "137:144=193:204"
	
	States
	{
		Spawn:
		FX01 C 0
		goto Main
	}
}


actor FireburnerSkill : HLifeForceWeapon replaces SkullRod
{
	Weapon.SlotNumber 5
	Obituary "%o was flamed by %k."
	Inventory.Icon "WEP5A0"
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("FireburnerPickup")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 5 )
		goto Super::Deselect

		Fire:
		TNT1 A 0
		goto Super::Fire.Burner
	}
}

actor HFireburnerProjectile: FireburnerProjectile replaces FireburnerProjectile
{
	Alpha 0.5
	RenderStyle Translucent
	
	States
	{
		Spawn:
		FIRE A 0
		
		Main:
		"####" ABCD 2 bright 

		Fireloop:
		"####" "#" 0 bright A_PlaySound("lifeforce/burnerhit")
		"####" EFGHABCD 2 bright A_Explode(2, 256, 0, false, 256)
		"####" IJKLM 2 bright
		stop

		Death:
		"####" "#" 0 A_Explode(2, 256, 0, false, 256)
		"####" IJKLM 2 bright
		stop
	}
}

actor LightningBoltSkill: HLifeForceWeapon replaces PhoenixRod
{
	Weapon.SlotNumber 6
	Obituary "%o was shocked by %k."
	Inventory.Icon "LPUFA0"
	DamageType "Lightning"
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("LightningBoltPickup")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 6 )
		goto Super::Deselect

		Fire:
		TNT1 A 0
		goto Super::Fire.Lightning

	}
}

actor DecimatorSkill : HLifeForceWeapon replaces Mace
{
	Weapon.SlotNumber 7
	Obituary "%o was decimated by %k."
	Inventory.Icon "WEP7A0"
	
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("DecimatorPickup")
		stop

		Deselect:
		TNT1 A 0 A_SetUserVar( "user_altwep", 7 )
		goto Super::Deselect
		
		Fire:
		goto Super::Fire.Decimator
	}
}

actor HDecimatorProjectile: DecimatorProjectile replaces DecimatorProjectile
{
	Translation "137:144=145:168", "241:247=145:168"
	Scale 0.75

	States
	{
		Spawn:
		FX15 ABCA 4 bright
		goto Super::FireLoop
	}
}

actor HDecimatorProjectile2: HDecimatorProjectile replaces DecimatorProjectile2
{
	Scale 1.0
	Damage (25)
}

actor HDecimatorProjectile3: HDecimatorProjectile replaces DecimatorProjectile3
{
	Scale 1.25
	Damage (45)
}
